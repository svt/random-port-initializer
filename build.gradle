plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.0"
    id 'org.jmailen.kotlinter' version '1.20.1'
    id 'pl.allegro.tech.build.axion-release' version '1.9.3'
    id "com.jfrog.bintray" version "1.8.4"
    id "com.github.ben-manes.versions" version '0.20.0' 
}

group = 'se.svt.util.spring'
project.version=scmVersion.version

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'java-library'

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['publishjob']
    pkg {
        repo = 'opensource'
        name = 'random-port-initializer'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/SVT/random-port-initializer'
        userOrg = 'svt'

        version {
            name = scmVersion.version
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "svt open source"
            organisation "SVT"
            organisationUrl "https://www.svt.se/opensource/"
            email "opensource@svt.se"
        }
    }
    
    scm {
       url "https://github.com/SVT/random-port-initializer/"
    }
}
publishing {
    publications {
        publishjob(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'se.svt.util.spring'
            artifactId 'random-port-initializer'
            version  scmVersion.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A Spring test util for resolving property placeholders to free random ports')
                root.appendNode('name', 'random-port-initializer')
                root.appendNode('url', 'https://github.com/SVT/random-port-initializer')
                root.children().last() + pomConfig
            }
        }
    }
}
compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

configurations.all {
    exclude module: 'junit'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task testJar(type: Jar, dependsOn: testClasses) {
    from sourceSets.test.output
}

publish.dependsOn check

dependencies {
    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.0'
    api 'org.jetbrains.kotlin:kotlin-reflect:1.3.0'
    api 'org.springframework:spring-context:5.1.2.RELEASE'

    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testImplementation 'org.springframework:spring-test:5.1.2.RELEASE'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

task wrapper(type: Wrapper) {
    distributionType = 'ALL'
    gradleVersion = '4.10.2'
}
